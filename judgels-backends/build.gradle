buildscript {
    repositories {
        mavenCentral()

        maven {
            url 'http://palantir.bintray.com/releases'
        }
    }

    dependencies {
        classpath "com.palantir.baseline:gradle-baseline-java:$gradleBaselineVersion"
    }
}

plugins {
    id 'idea'

    id 'com.palantir.git-version' version '0.8.0'
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()

        maven {
            url 'http://palantir.bintray.com/releases'
        }
        maven {
            url 'https://dl.bintray.com/judgels/releases'
        }
    }

    group 'org.iatoki.judgels'
    version gitVersion()
}

apply plugin: 'com.palantir.baseline-config'

dependencies {
    baseline "com.palantir.baseline:gradle-baseline-java-config:$gradleBaselineVersion@zip"
}

allprojects {
    version '2.5.0'
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'

    apply plugin: 'com.palantir.baseline-checkstyle'
    apply plugin: 'com.palantir.baseline-idea'

    configurations.each {
        it.resolutionStrategy {
            eachDependency { details ->
                if (details.requested.group.startsWith('com.fasterxml.jackson')) {
                    force "${details.requested.group}:${details.requested.name}:$jacksonVersion"
                }
            }
        }
    }

    tasks.withType(Test) {
        useJUnitPlatform()
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
        testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
    }

    idea.module {
        generatedSourceDirs += file('generated')
        sourceDirs += file('generated')
        testSourceDirs += file('generated_tests')
    }
}
