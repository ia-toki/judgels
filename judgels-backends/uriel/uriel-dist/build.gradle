plugins {
    id 'com.palantir.sls-java-service-distribution'
    id 'com.palantir.launch-config' version '1.1.0'
}

dependencies {
    runtimeOnly project(':michael:uriel-app')
}

distribution {
    serviceName 'uriel'
    serviceGroup 'judgels'
    mainClass 'judgels.uriel.UrielApplication'
    args 'server', 'var/conf/uriel.yml'
}

task dbStatus(type: JavaExec, dependsOn: jar) {
    group 'Database'
    description 'Checks database migration status.'

    classpath = sourceSets.main.runtimeClasspath
    main = 'judgels.uriel.UrielApplication'
    args = [ 'db', 'status', 'var/conf/uriel.yml' ]
}

task dbMigrate(type: JavaExec, dependsOn: jar) {
    group 'Database'
    description 'Migrates database to the latest version.'

    classpath = sourceSets.main.runtimeClasspath
    main = 'judgels.uriel.UrielApplication'
    args = [ 'db', 'migrate', 'var/conf/uriel.yml' ]
    systemProperty 'liquibase.databaseChangeLogTableName', 'uriel_DATABASECHANGELOG'
    systemProperty 'liquibase.databaseChangeLogLockTableName', 'uriel_DATABASECHANGELOGLOCK'
}

task dbRollbackOnce(type: JavaExec, dependsOn: jar) {
    group 'Database'
    description 'Rollbacks the latest change of the database.'

    classpath = sourceSets.main.runtimeClasspath
    main = 'judgels.uriel.UrielApplication'
    args = [ 'db', 'rollback', 'var/conf/uriel.yml', '-c', '1']
}

task dbDump(type: JavaExec, dependsOn: jar) {
    group 'Database'
    description 'Dumps current database schema.'

    classpath = sourceSets.main.runtimeClasspath
    main = 'judgels.uriel.UrielApplication'
    args = [ 'db', 'dump', 'var/conf/uriel.yml' ]
}
